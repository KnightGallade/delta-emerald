mapscripts PWT_PreBattleRoom_MapScripts {
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0: PWT_PreBattleRoom_EventScript_EnterRoom
    ] 
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [
        VAR_TEMP_1, 0: PWT_PreBattleRoom_EventScript_TurnPlayerNorth
    ]
}

script PWT_PreBattleRoom_EventScript_TurnPlayerNorth {
	setvar(VAR_TEMP_1, 1)
	turnobject(LOCALID_PLAYER, DIR_NORTH)
	end
}

script PWT_PreBattleRoom_EventScript_EnterRoom {
	goto_if_eq(VAR_0x8006, 1, PWT_PreBattleRoom_EventScript_ReturnFromBattle)
	delay(1)
	setvar(VAR_TEMP_0, 1)
	applymovement(LOCALID_PLAYER, moves(walk_up * 4))
	waitmovement(0)
	lockall
    goto(PWT_PreBattleRoom_EventScript_AskReadyForNextRound)
}

script PWT_PreBattleRoom_EventScript_AskReadyForNextRound {
	call(PWT_PreBattleRoom_EventScript_ReadyForNextRoundMessage)
	waitmessage
	dynmultichoice(16, 0, TRUE, 3, 0, DYN_MULTICHOICE_CB_NONE, "OPPONENT", "TOURNEY TREE", "READY TO START")
	switch (var(VAR_RESULT)) {
		case 0:
			goto(PWT_PreBattleRoom_EventScript_ShowOpponentInfo)
		case 1:
			goto(PWT_PreBattleRoom_EventScript_ShowTourneyTree)
		case 2:
			goto(PWT_PreBattleRoom_EventScript_ContinueChallenge)
	}
}

script PWT_PreBattleRoom_EventScript_ShowOpponentInfo {
	msgbox("{PLAYER}'s next opponent\nis this TRAINER.", MSGBOX_DEFAULT)
	fadescreen (FADE_TO_BLACK)
	pwt_showopponentinfo
	waitstate
	goto(PWT_PreBattleRoom_EventScript_AskReadyForNextRound)
}

script PWT_PreBattleRoom_EventScript_ShowTourneyTree {
	pwt_get(PWT_DATA_BATTLE_NUM)
	switch(var(VAR_RESULT)) {
		case PWT_ROUND1:
			msgbox("The tournament is in the 1st round.$", MSGBOX_DEFAULT)
		case PWT_ROUND2:
			msgbox("The tournament is in the 2nd round.$", MSGBOX_DEFAULT)
		case PWT_SEMIFINAL:
			msgbox("The tournament is in the semifinals.$", MSGBOX_DEFAULT)
		case PWT_FINAL:
			msgbox("The tournament is up to the final.$", MSGBOX_DEFAULT)
	}
	fadescreen(FADE_TO_BLACK)
	pwt_showtourneytree
	waitstate
	goto(PWT_PreBattleRoom_EventScript_AskReadyForNextRound)
}

script PWT_PreBattleRoom_EventScript_ContinueChallenge {
	message("Please choose the two POKÃ©MON\nthat are to appear in battle.")
	waitmessage
	waitbuttonpress
	fadescreen(FADE_TO_BLACK)
	// TODO - optimize to no longer have levels or smaller party size
	copyvar(VAR_0x8004, FRONTIER_LVL_OPEN)
	setvar(VAR_0x8005, 2) // For now, only 2/3
	special(ChoosePartyForPWT)
	waitstate
	pwt_resetsketchbeforebattle
	goto_if_eq(VAR_RESULT, 0, PWT_PreBattleRoom_EventScript_AskReadyForNextRound) // error during selection I imagine?
	pwt_set(PWT_DATA_SELECTED_MONS)
	pwt_reduceparty
	pwt_setopponent
	msgbox("Right this way, please.", MSGBOX_DEFAULT)
	closemessage
	applymovement(LOCALID_PWT_PRE_BATTLE_ATTENDANT, moves(walk_right, face_left))
	waitmovement(0)
	applymovement(LOCALID_PLAYER, moves(walk_up))
	waitmovement(0)
	opendoor(5, 1)
	waitdooranim
	applymovement(LOCALID_PLAYER, moves(walk_up, set_invisible))
	waitmovement(0)
	closedoor(5, 1)
	waitdooranim
	warp(MAP_PWT_BATTLE_ROOM, 9, 5)
	setvar(VAR_TEMP_0, 0)
	waitstate
	end
}

script PWT_PreBattleRoom_EventScript_ReadyForNextRoundMessage {
	pwt_get(PWT_DATA_BATTLE_NUM)
    switch (var(VAR_RESULT)) {
        // case PWT_ROUND1: since round complete, can't be on first round
        case PWT_ROUND1:
            message("Your 1st-round match is next.\nAre you ready?")
        case PWT_ROUND2:
            message("Your 2nd-round match is next.\nAre you ready?")
        case PWT_SEMIFINAL:
            message("Your semifinal match is next.\nAre you ready?")
        case PWT_FINAL:
			message("Your final match is next.\nAre you ready?")
    }
    return
}

script PWT_PreBattleRoom_EventScript_ReturnFromBattle {
	setvar(VAR_TEMP_0, 1)
	msgbox("Time to heal", MSGBOX_DEFAULT)
	special(LoadPlayerParty)
	pwt_setpartyorder(PWT_PARTY_SIZE)
	playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
	call(PWT_PreBattleRoom_EventScript_RoundCompleteMessage)
	fadescreen(FADE_TO_BLACK)
	pwt_showstatictourneytree
	waitstate
	goto(PWT_PreBattleRoom_EventScript_AskReadyForNextRound)
}

script PWT_PreBattleRoom_EventScript_RoundCompleteMessage {
	pwt_get(PWT_DATA_BATTLE_NUM)
    switch (var(VAR_RESULT)) {
        // case PWT_ROUND1: since round complete, can't be on first round
        case PWT_ROUND2:
            msgbox("All 1st-round matches have been\ncompleted.\pThese are the teams that advanced!", MSGBOX_DEFAULT)
        case PWT_SEMIFINAL:
            msgbox("All 2nd-round matches have been\ncompleted.\pThese are the teams that advanced!", MSGBOX_DEFAULT)
        case PWT_FINAL:
            msgbox("All semifinal matches have been\ncompleted.\pThese are the teams that advanced!", MSGBOX_DEFAULT)
    }
    return
}
