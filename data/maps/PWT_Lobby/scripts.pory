mapscripts PWT_Lobby_MapScripts {
    // TODO - handle results of a random tournament
    // TODO - handle forced save stuff
}


script PWT_Lobby_EventScript_Attendant {
	lock
	faceplayer
	setvar(VAR_FRONTIER_FACILITY, FACILITY_PWT) // TODO - set the define to allow this, but need to figure out if necessary
	special(SavePlayerParty)
    setspeaker("Attendant")
    msgbox(PWT_Lobby_Text_Introduction, MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_AskTakeChallenge)
}

script PWT_Lobby_EventScript_AskTakeChallenge {
    setspeaker("Attendant")
    message("Would you like to enter?")
    waitmessage
    dynmultichoice(25, 0, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Enter", "Info", "Exit")
    switch (var(VAR_RESULT)) {
        case 0:
            goto(PWT_Lobby_EventScript_CheckElligibility)
        case 1:
            msgbox(PWT_Lobby_Text_ExplainChallenge, MSGBOX_DEFAULT)
            goto(PWT_Lobby_EventScript_AskTakeChallenge) // Start over loop
        case 2:
        case MULTI_B_PRESSED:
            goto(PWT_Lobby_EventScript_CancelChallenge)
    }
}

script PWT_Lobby_EventScript_CheckElligibility {
    // First check if enough Pokémon are in team
    specialvar(VAR_RESULT, CountPartyNonEggMons)
    goto_if_lt(VAR_RESULT, 6, PWT_Lobby_EventScript_NotEnoughMons)
    // Next, check if enough valid Pokémon are in team
    pwt_CheckIneligible // TODO - write eligibility check
    goto_if_eq(VAR_RESULT, TRUE, PWT_Lobby_EventScript_NotEligible)
    goto(PWT_Lobby_EventScript_TryEnterChallenge)
}

script PWT_Lobby_EventScript_TryEnterChallenge {
    setspeaker("Attendant")
    message("In which tournament would you like to\nparticipate?")
    waitmessage
    dynmultichoice(25, 0, FALSE, 4, 0, DYN_MULTICHOICE_CB_NONE, "Indigo", "Hoenn", "Royale", "Exit") // TODO - add more options
    switch (var(VAR_RESULT)) {
        case 0:
            pwt_SetData(PWT_DATA_TOURNAMENT_TYPE, PWT_TOURNAMENT_TYPE_INDIGO_LEAGUE)
        case 1:
            pwt_SetData(PWT_DATA_TOURNAMENT_TYPE, PWT_TOURNAMENT_TYPE_HOENN)
        case 2:
            pwt_SetData(PWT_DATA_TOURNAMENT_TYPE, PWT_TOURNAMENT_TYPE_ROYALE)
        case 3:
        case MULTI_B_PRESSED:
            goto(PWT_Lobby_EventScript_CancelChallenge)
    }
    setspeaker("Attendant")
    message("In what battle style would you like to\nparticipate?")
    waitmessage
    dynmultichoice(25, 0, FALSE, 5, 0, DYN_MULTICHOICE_CB_NONE, "Singles", "Doubles", "Multi", "Link", "Exit")
    switch (var(VAR_RESULT)) {
        case 0:
            pwt_SetData(PWT_DATA_BATTLE_STYLE, PWT_BATTLE_STYLE_SINGLES)
        case 1:
            pwt_SetData(PWT_DATA_BATTLE_STYLE, PWT_BATTLE_STYLE_DOUBLES)
        case 2: // TODO - handle multi-battles
        case 3: // TODO - handle link battles
        case 4:
        case MULTI_B_PRESSED:
            goto(PWT_Lobby_EventScript_CancelChallenge)
    }
    goto(PWT_Lobby_EventScript_PrepareTournament)
}

script PWT_Lobby_EventScript_PrepareTournament {
    // Prepare player party
    callnative(RemoveFollowingPokemon)
    special(SavePlayerParty)
    // Separated the initialization functions for debugging purposes
    pwt_init
    pwt_InitializeTrainers
    pwt_SetTrainers
    setspeaker("Attendant")
    msgbox(PWT_Lobby_Text_LetTheTournamentBegin, MSGBOX_DEFAULT)
    closemessage
    call(PWT_Lobby_EventScript_WalkToDoor)
    special(HealPlayerParty)
    warp(MAP_PWT_CORRIDOR, 23, 6)
    waitstate
    end
}

script PWT_Lobby_EventScript_WalkToDoor {
    applymovement(LOCALID_PWT_ATTENDANT, moves(walk_up * 5))
	applymovement(LOCALID_PLAYER, moves(walk_up * 5))
	waitmovement(0)
	opendoor(5, 4)
	waitdooranim
	applymovement(LOCALID_PWT_ATTENDANT, moves(walk_up, set_invisible))
	applymovement(LOCALID_PLAYER, moves(walk_up * 2, set_invisible))
	waitmovement(0)
	closedoor(5, 4)
	waitdooranim
	return
}

// ---------- ELIGIBILITY SCRIPTS ---------- 
script PWT_Lobby_EventScript_NotEnoughMons {
    msgbox(PWT_Lobby_Text_NotEnoughMons, MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_ComeBackLater)
}

script PWT_Lobby_EventScript_NotEligible {
    // Figure out how to print invalids
    msgbox("TMP INELIGIBLE MSG", MSGBOX_DEFAULT)
    // TODO - figure out how this works
    // call(BattleFrontier_ShowCaughtBannedSpecies)
    goto(PWT_Lobby_EventScript_ComeBackLater)
}

script PWT_Lobby_EventScript_ComeBackLater {
    msgbox(PWT_Lobby_Text_ComeBackLater, MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_EndCancelChallenge)
}

// ---------- CANCELLING SCRIPTS ---------- 

// TODO - verify this function is ever called
script PWT_Lobby_EventScript_LoadPartyCancelChallenge {
    special(LoadPlayerParty)
    callnative(UpdateFollowingPokemon)
    goto(PWT_Lobby_EventScript_CancelChallenge)
}

script PWT_Lobby_EventScript_CancelChallenge {
    msgbox("We hope to see you again.", MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_EndCancelChallenge)
}

script PWT_Lobby_EventScript_EndCancelChallenge {
	release
	end
}


//---------- TEXTS ----------
text PWT_Lobby_Text_Introduction {
    "Where legends across the globe for the\n
    title of World Champion!\p
    Welcome to the PWT,\n
    the POKéMON World Tournament!"
}

text PWT_Lobby_Text_ExplainChallenge {
    "The PWT supports all manners of\n
    tournaments and battle styles.\p
    To begin, you are required to have six\n
    POKéMON, without any duplicate species\l
    or held items.\p
    Levels are irrelevant as any and all PWT\n
    battles will set levels to 50.\p
    Then, the trainer bracket will be\n
    decided depending on the tournament\l
    you entered.\p
    Before every battle, you will be given a\n
    chance to see the team of all participating\l
    trainers.\p
    This naturally includes your next\n
    opponent.\p
    You will then be asked to select a party\n
    of three POKéMON from your six for the\l
    upcoming battle.\p
    The winner of the battle will move on,\n
    and we repeat until we have a winner!"
}

text PWT_Lobby_Text_LetTheTournamentBegin {
    "Then follow me, and the let the\n
    tournament begin!"
}

text PWT_Lobby_Text_NotEnoughMons {
    "Excuse me!\p
    You must have a full party of six\n
    Pokémon to enter."
}

text PWT_Lobby_Text_ComeBackLater {
    "Please come see me when you are ready."
}