mapscripts PWT_Lobby_MapScripts {}

script PWT_Lobby_EventScript_Attendant {
    lock
    faceplayer
    msgbox("Which battle format would\nyou like to try?", MSGBOX_DEFAULT)
    waitmessage
    dynmultichoice(17, 6, FALSE, 3, 0, DYN_MULTICHOICE_CB_NONE, "Singles", "Doubles","Cancel")
    switch (var(VAR_RESULT)) {
        case 0:
            setvar(VAR_FRONTIER_FACILITY, FACILITY_PWT)
            setvar(VAR_FRONTIER_BATTLE_MODE, PWT_MODE_SINGLES) // VAR_FRONTIER_BATTLE_MODE used here, despite not frontier
        case 1:
            setvar(VAR_FRONTIER_FACILITY, FACILITY_PWT)
            setvar(VAR_FRONTIER_BATTLE_MODE, PWT_MODE_DOUBLES)
        case 2:
        case MULTI_B_PRESSED:
            msgbox("Cancelling", MSGBOX_DEFAULT)
            goto(PWT_Lobby_EventScript_PWT_End)
    }
    special(SavePlayerParty)
    pwt_checkineligible
    goto_if_eq(VAR_0x8004, TRUE, PWT_Lobby_EventScript_PWT_NotEnoughValidMons)
    msgbox("Pick your mons", MSGBOX_DEFAULT)
    fadescreen(FADE_TO_BLACK)
	callnative(RemoveFollowingPokemon)
    // Keep this here for now for selection screen
    // TODO - better optimize in the future
    setvar(VAR_0x8004, FRONTIER_LVL_OPEN)
    // TODO - fix this to have all options later
    switch (var(VAR_FRONTIER_BATTLE_MODE)) {
        case PWT_MODE_SINGLES:
            setvar(VAR_0x8005, PWT_PARTY_SIZE)
        case PWT_MODE_DOUBLES:
            setvar(VAR_0x8005, PWT_DOUBLES_PARTY_SIZE)
    }
    special(ChoosePartyForPWT)
    waitstate
    goto_if_eq(VAR_RESULT, 0, PWT_Lobby_EventScript_PWT_LoadPartyCancel)
	msgbox("Ready to save?", MSGBOX_YESNO)
	switch (var(VAR_RESULT)) {
        case NO:
        case MULTI_B_PRESSED:
            goto(PWT_Lobby_EventScript_PWT_LoadPartyCancel)
        case YES:
            goto(PWT_Lobby_EventScript_PWT_SaveBeforeChallenge)
    }
}

script PWT_Lobby_EventScript_PWT_SaveBeforeChallenge {
    pwt_set(PWT_DATA_SELECTED_MON_ORDER)
    pwt_init
    special(LoadPlayerParty)
	closemessage
	delay(2)
	call(Common_EventScript_SaveGame)
    setvar(VAR_TEMP_CHALLENGE_STATUS, 255)
	goto_if_eq(VAR_RESULT, 0, PWT_Lobby_EventScript_PWT_CancelChallengeSaveFailed)
    // get party size
    switch (var(VAR_FRONTIER_BATTLE_MODE)) {
        case PWT_MODE_SINGLES:
            setvar(VAR_0x8005, PWT_PARTY_SIZE)
        case PWT_MODE_DOUBLES:
            setvar(VAR_0x8005, PWT_DOUBLES_PARTY_SIZE)
    }
    pwt_inittrainers
    msgbox("TMP: Tournament\nInitialized", MSGBOX_DEFAULT) // TODO - this fails half the time
    goto(PWT_Lobby_EventScript_PWT_EnterChallenge)
}

script PWT_Lobby_EventScript_PWT_EnterChallenge {
	special(SavePlayerParty)
	pwt_setpartyorder(PWT_PARTY_SIZE)
	pwt_settrainers
	msgbox("Time to go", MSGBOX_DEFAULT)
    closemessage
    call(PWT_Lobby_EventScript_WalkToDoor)
	special(HealPlayerParty)
	warp(MAP_PWT_CORRIDOR, 23, 6)
	setvar(VAR_TEMP_CHALLENGE_STATUS, 0)
	waitstate
	end
}

// separate function to eventually handle different attendants
script PWT_Lobby_EventScript_WalkToDoor {
    applymovement(LOCALID_PWT_ATTENDANT, moves(walk_up * 5))
	applymovement(LOCALID_PLAYER, moves(walk_up * 5))
	waitmovement(0)
	opendoor(5, 4)
	waitdooranim
	applymovement(LOCALID_PWT_ATTENDANT, moves(walk_up, set_invisible))
	applymovement(LOCALID_PLAYER, moves(walk_up * 2, set_invisible))
	waitmovement(0)
	closedoor(5, 4)
	waitdooranim
	return
}

script PWT_Lobby_EventScript_PWT_CancelChallengeSaveFailed {
    msgbox("No save means no play", MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_PWT_End)
}

script PWT_Lobby_EventScript_PWT_LoadPartyCancel {
    special(LoadPlayerParty)
	callnative(UpdateFollowingPokemon)
    msgbox("Error during processing", MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_PWT_End)
}

script PWT_Lobby_EventScript_PWT_NotEnoughValidMons {
    msgbox("Not Enough Valid Mons", MSGBOX_DEFAULT)
    goto(PWT_Lobby_EventScript_PWT_End)
}

script PWT_Lobby_EventScript_PWT_End {
    special(CloseLink) // For multi
    release
    end
}
